buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3")
        classpath("net.saliman:gradle-cobertura-plugin:2.3.2")
    }
}



allprojects {
    ext.version_base = '1.0.0'
    version = version_base

    apply plugin: 'net.saliman.cobertura'
    apply plugin: "com.github.kt3k.coveralls"

    cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
    cobertura.coverageIgnoreTrivial = true // do not worry about getters and setters
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'spring-boot'

    group = 'uk.ac.ebi.subs'

    dependencies {
        compile("org.springframework.boot:spring-boot-devtools")
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '2.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task copyConfiguration << {
        println "Target environment: $environment"

        copy {
            from "src/main/environment/resources/${environment}"
            into "src/main/resources"
            include "**/*"
        }
    }

    task copyTestResourcesConfiguration << {
        println "Copying test resources for environment : ${environment}"

        copy {
            from "src/test/environment/resources/${environment}"
            into "src/test/resources"
            include "**/*"
        }
    }


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourceJar(type: Jar) { from sourceSets.main.allJava }

}